From Equations Require Import Equations.

From Mctt Require Import LibTactics.
From Mctt.Core Require Import Base.
From Mctt.Core.Semantic Require Import Readback Evaluation.
From Mctt.Extraction Require Import Evaluation.
Import Domain_Notations.

Generalizable All Variables.

Inductive read_nf_order : nat -> domain_nf -> Prop :=
| rnf_type :
  `( read_typ_order s a ->
    read_nf_order s d{{{ â‡“ ð•Œ@i a }}} )
| rnf_zero :
  `( read_nf_order s d{{{ â‡“ â„• zero }}} )
| rnf_succ :
  `( read_nf_order s d{{{ â‡“ â„• m }}} ->
     read_nf_order s d{{{ â‡“ â„• (succ m) }}} )
| rnf_nat_neut :
  `( read_ne_order s m ->
     read_nf_order s d{{{ â‡“ â„• (â‡‘ a m) }}} )
| rnf_fn :
  `( read_typ_order s a ->
     eval_app_order m d{{{ â‡‘! a s }}} ->
     eval_exp_order B d{{{ p â†¦ â‡‘! a s }}} ->
     (forall m' b,
         {{ $| m & â‡‘! a s |â†˜ m' }} ->
         {{ âŸ¦ B âŸ§ p â†¦ â‡‘! a s â†˜ b }} ->
         read_nf_order (S s) d{{{ â‡“ b m' }}}) ->
     read_nf_order s d{{{ â‡“ (Î  a p B) m }}} )
| rnf_pair :
  `( read_typ_order s a ->
     eval_exp_order B d{{{ p â†¦ â‡‘! a s }}} ->
     eval_fst_order m ->
     eval_snd_order m ->
     (forall b,
          {{ âŸ¦ B âŸ§ p â†¦ â‡‘! a s â†˜ b }} ->
          read_typ_order (S s) b) ->
     (forall m1,
         {{ Ï€â‚ m â†˜ m1 }} ->
         eval_exp_order B d{{{ p â†¦ m1 }}} ) ->
     (forall m1,
         {{ Ï€â‚ m â†˜ m1 }} ->
         read_nf_order s d{{{ â‡“ a m1 }}}) ->
     (forall m1 m2 b,
         {{ Ï€â‚ m â†˜ m1 }} ->
         {{ Ï€â‚‚ m â†˜ m2 }} ->
         {{ âŸ¦ B âŸ§ p â†¦ m1 â†˜ b }} ->
         read_nf_order s d{{{ â‡“ b m2 }}}) ->
     read_nf_order s d{{{ â‡“ (Î£ a p B) m }}} )
| rnf_refl :
  `( read_typ_order s a ->
     read_nf_order s d{{{ â‡“ a m' }}} ->
     read_nf_order s d{{{ â‡“ (Eq a m1 m2) (refl m') }}} )
| rnf_eq_neut :
  `( read_ne_order s n ->
     read_nf_order s d{{{ â‡“ (Eq a m1 m2) (â‡‘ b n) }}} )
| rnf_neut :
  `( read_ne_order s m ->
     read_nf_order s d{{{ â‡“ (â‡‘ a b) (â‡‘ c m) }}} )

with read_ne_order : nat -> domain_ne -> Prop :=
| rne_var :
  `( read_ne_order s d{{{ !x }}} )
| rne_app :
  `( read_ne_order s m ->
     read_nf_order s n ->
     read_ne_order s d{{{ m n }}} )
| rne_fst : 
  `( read_ne_order s m ->
     read_ne_order s d{{{ fst m }}} )
| rne_snd :
  `( read_ne_order s m ->
     read_ne_order s d{{{ snd m }}} )
| rne_natrec :
  `( eval_exp_order B d{{{ p â†¦ â‡‘! â„• s }}} ->
     (forall b,
         {{ âŸ¦ B âŸ§ p â†¦ â‡‘! â„• s â†˜ b }} ->
         read_typ_order (S s) b) ->
     eval_exp_order B d{{{ p â†¦ zero }}} ->
     (forall bz,
         {{ âŸ¦ B âŸ§ p â†¦ zero â†˜ bz }} ->
         read_nf_order s d{{{ â‡“ bz mz }}}) ->
     eval_exp_order B d{{{ p â†¦ succ (â‡‘! â„• s) }}} ->
     (forall b,
         {{ âŸ¦ B âŸ§ p â†¦ â‡‘! â„• s â†˜ b }} ->
         eval_exp_order MS d{{{ p â†¦ â‡‘! â„• s â†¦ â‡‘! b (S s) }}}) ->
     (forall b bs ms,
         {{ âŸ¦ B âŸ§ p â†¦ â‡‘! â„• s â†˜ b }} ->
         {{ âŸ¦ B âŸ§ p â†¦ succ (â‡‘! â„• s) â†˜ bs }} ->
         {{ âŸ¦ MS âŸ§ p â†¦ â‡‘! â„• s â†¦ â‡‘! b (S s) â†˜ ms }} ->
         read_nf_order (S (S s)) d{{{ â‡“ bs ms }}}) ->
     read_ne_order s m ->
     read_ne_order s d{{{ rec m under p return B | zero -> mz | succ -> MS end }}} )
| read_ne_eqrec :
  `( read_typ_order s a ->
     read_nf_order s d{{{ â‡“ a m1 }}} ->
     read_nf_order s d{{{ â‡“ a m2 }}} ->
     eval_exp_order B d{{{ p â†¦ â‡‘! a s â†¦ â‡‘! a (S s) â†¦ â‡‘! (Eq a (â‡‘! a s) (â‡‘! a (S s))) (S (S s)) }}} ->
     (forall b,
         {{ âŸ¦ B âŸ§ p â†¦ â‡‘! a s â†¦ â‡‘! a (S s) â†¦ â‡‘! (Eq a (â‡‘! a s) (â‡‘! a (S s))) (S (S s)) â†˜ b }} ->
         read_typ_order (S (S (S s))) b) ->
     eval_exp_order B d{{{ p â†¦ â‡‘! a s â†¦ â‡‘! a s â†¦ refl (â‡‘! a s) }}} ->
     eval_exp_order BR d{{{ p â†¦ â‡‘! a s }}} ->
     (forall b br,
         {{ âŸ¦ B âŸ§ p â†¦ â‡‘! a s â†¦ â‡‘! a s â†¦ refl (â‡‘! a s) â†˜ b }} ->
         {{ âŸ¦ BR âŸ§ p â†¦ â‡‘! a s â†˜ br }} ->
         read_nf_order (S s) d{{{ â‡“ b br }}}) ->
     read_ne_order s n ->
     read_ne_order s d{{{ eqrec n under p as Eq a m1 m2 return B | refl -> BR end }}} )

with read_typ_order : nat -> domain -> Prop :=
| rtyp_univ :
  `( read_typ_order s d{{{ ð•Œ@i }}} )
| rtyp_nat :
  `( read_typ_order s d{{{ â„• }}} )
| rtyp_pi :
  `( read_typ_order s a ->
     eval_exp_order B d{{{ p â†¦ â‡‘! a s }}} ->
     (forall b,
         {{ âŸ¦ B âŸ§ p â†¦ â‡‘! a s â†˜ b }} ->
         read_typ_order (S s) b) ->
     read_typ_order s d{{{ Î  a p B }}})
| rtyp_sigma :
  `( read_typ_order s a ->
     eval_exp_order B d{{{ p â†¦ â‡‘! a s }}} ->
     (forall b,
         {{ âŸ¦ B âŸ§ p â†¦ â‡‘! a s â†˜ b }} ->
         read_typ_order (S s) b) ->
     read_typ_order s d{{{ Î£ a p B }}})
| rtyp_eq :
  `( read_typ_order s a ->
     read_nf_order s d{{{ â‡“ a m1 }}} ->
     read_nf_order s d{{{ â‡“ a m2 }}} ->
     read_typ_order s d{{{ Eq a m1 m2 }}})
| rtyp_neut :
  `( read_ne_order s b ->
     read_typ_order s d{{{ â‡‘ a b }}} ).

#[local]
Hint Constructors read_nf_order read_ne_order read_typ_order : mctt.

Lemma read_nf_order_sound : forall s d m,
    {{ Rnf d in s â†˜ m }} ->
    read_nf_order s d
with read_ne_order_sound : forall s d m,
    {{ Rne d in s â†˜ m }} ->
    read_ne_order s d
with read_typ_order_sound : forall s d m,
    {{ Rtyp d in s â†˜ m }} ->
    read_typ_order s d.
Proof with (econstructor; intros; functional_eval_rewrite_clear; mauto).
  - clear read_nf_order_sound; induction 1...
  - clear read_ne_order_sound; induction 1...
  - clear read_typ_order_sound; induction 1...
Qed.

#[export]
Hint Resolve read_nf_order_sound read_ne_order_sound read_typ_order_sound : mctt.

#[local]
Ltac impl_obl_tac1 :=
  match goal with
  | H : read_nf_order _ _ |- _ => progressive_invert H
  | H : read_ne_order _ _ |- _ => progressive_invert H
  | H : read_typ_order _ _ |- _ => progressive_invert H
  end.

#[local]
Ltac impl_obl_tac :=
  repeat impl_obl_tac1; try econstructor; mauto.

#[tactic="impl_obl_tac",derive(equations=no,eliminator=no)]
Equations read_nf_impl s d (H : read_nf_order s d) : { m | {{ Rnf d in s â†˜ m }} } by struct H :=
| s, d{{{ â‡“ ð•Œ@i a }}}, H =>
    let (A, HA) := read_typ_impl s a _ in
    exist _ A _
| s, d{{{ â‡“ â„• zero }}}    , H => exist _ n{{{ zero }}} _
| s, d{{{ â‡“ â„• (succ m) }}}, H =>
    let (M, HM) := read_nf_impl s d{{{ â‡“ â„• m }}} _ in
    exist _ n{{{ succ M }}} _
| s, d{{{ â‡“ â„• (â‡‘ ^_ m) }}}, H =>
    let (M, HM) := read_ne_impl s m _ in
    exist _ n{{{ â‡‘ M }}} _
| s, d{{{ â‡“ (Î  a p B) m }}}, H =>
    let (A, HA) := read_typ_impl s a _ in
    let (m', Hm') := eval_app_impl m d{{{ â‡‘! a s }}} _ in
    let (b, Hb) := eval_exp_impl B d{{{ p â†¦ â‡‘! a s }}} _ in
    let (M, HM) := read_nf_impl (S s) d{{{ â‡“ b m' }}} _ in
    exist _ n{{{ Î» A M }}} _
| s, d{{{ â‡“ (Î£ a p B) m }}}, H =>
    let (A, HA) := read_typ_impl s a _ in
    let (b, Hb) := eval_exp_impl B d{{{ p â†¦ â‡‘! a s }}} _ in
    let (B', HB) := read_typ_impl (S s) b _ in
    let (m1, Hm1) := eval_fst_impl m _ in
    let (m2, Hm2) := eval_snd_impl m _ in
    let (M, HM) := read_nf_impl s d{{{ â‡“ a m1 }}} _ in
    let (b', Hb') := eval_exp_impl B d{{{ p â†¦ m1 }}} _ in
    let (N, HN) := read_nf_impl s d{{{ â‡“ b' m2 }}} _ in
    exist _ n{{{ âŸ¨ M : A ; N : B' âŸ© }}} _
| s, d{{{ â‡“ (Eq a m1 m2) (refl m') }}}, H =>
    let (A, HA) := read_typ_impl s a _ in
    let (M', HM') := read_nf_impl s d{{{ â‡“ a m' }}} _ in
    exist _ n{{{ refl A M' }}} _
| s, d{{{ â‡“ (Eq a m1 m2) (â‡‘ b n) }}}, H =>
    let (N, HN) := read_ne_impl s n _ in
    exist _ n{{{ â‡‘ N }}} _
| s, d{{{ â‡“ (â‡‘ a b) (â‡‘ c m) }}}, H =>
    let (M, HM) := read_ne_impl s m _ in
    exist _ n{{{ â‡‘ M }}} _

with read_ne_impl s d (H : read_ne_order s d) : { m | {{ Rne d in s â†˜ m }} } by struct H :=
| s, d{{{ !x }}}, H => exist _ n{{{ #(s - x - 1) }}} _
| s, d{{{ m n }}}, H =>
    let (M, HM) := read_ne_impl s m _ in
    let (N, HN) := read_nf_impl s n _ in
    exist _ n{{{ M N }}} _
| s, d{{{ fst m }}}, H =>
    let (M, HM) := read_ne_impl s m _ in
    exist _ n{{{ fst M }}} _
| s, d{{{ snd m }}}, H =>
    let (M, HM) := read_ne_impl s m _ in
    exist _ n{{{ snd M }}} _
| s, d{{{ rec m under p return B | zero -> mz | succ -> MS end }}}, H =>
    let (b, Hb) := eval_exp_impl B d{{{ p â†¦ â‡‘! â„• s }}} _ in
    let (B', HB') := read_typ_impl (S s) b _ in
    let (bz, Hbz) := eval_exp_impl B d{{{ p â†¦ zero }}} _ in
    let (MZ, HMZ) := read_nf_impl s d{{{ â‡“ bz mz }}} _ in
    let (bs, Hbs) := eval_exp_impl B d{{{ p â†¦ succ (â‡‘! â„• s) }}} _ in
    let (ms, Hms) := eval_exp_impl MS d{{{ p â†¦ â‡‘! â„• s â†¦ â‡‘! b (S s) }}} _ in
    let (MS', HMS') := read_nf_impl (S (S s)) d{{{ â‡“ bs ms }}} _ in
    let (M, HM) := read_ne_impl s m _ in
    exist _ n{{{ rec M return B' | zero -> MZ | succ -> MS' end }}} _
| s, d{{{ eqrec n under p as Eq a m1 m2 return B | refl -> BR end }}}, H =>
    let (A, HA) := read_typ_impl s a _ in
    let (M1, HM1) := read_nf_impl s d{{{ â‡“ a m1 }}} _ in
    let (M2, HM2) := read_nf_impl s d{{{ â‡“ a m2 }}} _ in
    let (b, Hb) := eval_exp_impl B d{{{ p â†¦ â‡‘! a s â†¦ â‡‘! a (S s) â†¦ â‡‘! (Eq a (â‡‘! a s) (â‡‘! a (S s))) (S (S s)) }}} _ in
    let (B', HB') := read_typ_impl (S (S (S s))) b _ in
    let (bbr, Hbbr) := eval_exp_impl B d{{{ p â†¦ â‡‘! a s â†¦ â‡‘! a s â†¦ refl (â‡‘! a s) }}} _ in
    let (br, Hbr) := eval_exp_impl BR d{{{ p â†¦ â‡‘! a s }}} _ in
    let (BR', HBR') := read_nf_impl (S s) d{{{ â‡“ bbr br }}} _ in
    let (N, HN) := read_ne_impl s n _ in
    exist _ n{{{ eqrec N as Eq A M1 M2 return B' | refl -> BR' end }}} _

with read_typ_impl s d (H : read_typ_order s d) : { m | {{ Rtyp d in s â†˜ m }} } by struct H :=
| s, d{{{ ð•Œ@i }}}, H => exist _ n{{{ Type@i }}} _
| s, d{{{ â„• }}}, H => exist _ n{{{ â„• }}} _
| s, d{{{ Î  a p B }}}, H =>
    let (A, HA) := read_typ_impl s a _ in
    let (b, Hb) := eval_exp_impl B d{{{ p â†¦ â‡‘! a s }}} _ in
    let (B', HB') := read_typ_impl (S s) b _ in
    exist _ n{{{ Î  A B' }}} _
| s, d{{{ Î£ a p B }}}, H =>
    let (A, HA) := read_typ_impl s a _ in
    let (b, Hb) := eval_exp_impl B d{{{ p â†¦ â‡‘! a s }}} _ in
    let (B', HB') := read_typ_impl (S s) b _ in
    exist _ n{{{ Î£ A B' }}} _
| s, d{{{ Eq a m1 m2 }}}, H =>
    let (A, HA) := read_typ_impl s a _ in
    let (M1, HM1) := read_nf_impl s d{{{ â‡“ a m1 }}} _ in
    let (M2, HM2) := read_nf_impl s d{{{ â‡“ a m2 }}} _ in
    exist _ n{{{ Eq A M1 M2 }}} _
| s, d{{{ â‡‘ a b }}}, H =>
    let (B, HB) := read_ne_impl s b _ in
    exist _ n{{{ â‡‘ B }}} _.

Extraction Inline read_nf_impl_functional
  read_ne_impl_functional
  read_typ_impl_functional.

(** The definitions of [read_*_impl] already come with soundness proofs,
    so we only need to prove completeness. However, the completeness
    is also obvious from the soundness of eval orders and functional
    nature of readback. *)

#[local]
Ltac functional_read_complete :=
  lazymatch goal with
  | |- exists (_ : ?T), _ =>
      let Horder := fresh "Horder" in
      assert T as Horder by mauto 3;
      eexists Horder;
      lazymatch goal with
      | |- exists _, ?L = _ =>
          destruct L;
          functional_read_rewrite_clear;
          eexists; reflexivity
      end
  end.

Lemma read_nf_impl_complete : forall s d m,
    {{ Rnf d in s â†˜ m }} ->
    exists H H', read_nf_impl s d H = exist _ m H'.
Proof.
  intros; functional_read_complete.
Qed.

Lemma read_ne_impl_complete : forall s d m,
    {{ Rne d in s â†˜ m }} ->
    exists H H', read_ne_impl s d H = exist _ m H'.
Proof.
  intros; functional_read_complete.
Qed.

Lemma read_typ_impl_complete : forall s d m,
    {{ Rtyp d in s â†˜ m }} ->
    exists H H', read_typ_impl s d H = exist _ m H'.
Proof.
  intros; functional_read_complete.
Qed.
